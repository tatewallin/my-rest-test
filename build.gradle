buildscript {
    ext {
        springBootVersion = "1.5.13.RELEASE"
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

repositories {
    maven {
        url("$artifactoryUrl/libs-release")
    }
}

// plugins to support CI Build Pipeline
apply plugin: 'com.mutualofomaha.jenkins-conventions'
apply plugin: 'com.mutualofomaha.war-conventions'
//apply plugin: 'com.mutualofomaha.groovy-conventions'

// plugins to support creating a Spring Boot app in Groovy
apply plugin: 'org.springframework.boot'
apply plugin: 'groovy'

// TODO Are these really needed?
apply plugin: 'eclipse'
apply plugin: 'idea'


// let the GroovyC compiler handle compilation for both java and groovy sources
sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

allprojects {
    plugins.withId('groovy') {
        sourceSets {
            main {
                // Needed for clover plugin with gradle 4
                output.classesDir = file("build/classes/groovy/main")
            }
        }
    }
}

dependencies {
    // Application dependencies. Use Gradle dependencies for details

    // Spring Parent poms
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-security")
    compile('org.springframework.boot:spring-boot-starter-data-jpa')

    compile('org.codehaus.groovy:groovy-all:2.4.9')
    compile('net.bull.javamelody:javamelody-core:1.63.0')

    // Rest API Documentation Support
    compile('io.springfox:springfox-swagger2:2.4.0')
    compile('io.springfox:springfox-swagger-ui:2.4.0')

    // database
    compile('cn.easyproject:ojdbc7:12.1.0.2.0')

    // security
    compile("commons-codec:commons-codec:1.9")

    // provided runtime for Tomcat e.g. we don't want to package embedded
    // Tomcat in our WAR file.
    providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')

    // Test dependencies for JUnit & Spock
    compile('cglib:cglib-nodep:3.2.0')  
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4-rc-3')
    testCompile('org.spockframework:spock-spring:1.1-groovy-2.4-rc-3')
    testCompile( 'com.athaydes:spock-reports:1.2.13' ) {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }

    testCompile('org.apache.httpcomponents:httpclient:4.3')  // Http Components for making Restful calls
}

// Following code is for defining your Jenkins CI Build Pipeline
pipelineConfig {
    asi = "GLD"
    projectName = "my-rest-test"
    buildPipelineRows = 10

    createPipeline {
        initJob()
        buildAndUnitTestJob()
        staticCodeAnalysisJob()
        publishSnapshotJob()
        promoteToReleaseCandidateJob()
        promoteToReleaseJob()
    }
}